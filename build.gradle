apply plugin: 'java'
apply plugin: 'project-report'

repositories {
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

configurations {
    testPlugins {}
}

configurations.all*.exclude group: 'xalan'

dependencies {

    compile 'org.glassfish.jersey.media:jersey-media-sse:2.25',
            'org.glassfish.jersey.media:jersey-media-json-jackson:2.25'

    testCompile 'org.jvnet:tiger-types:2.2',
                'org.jenkins-ci.main:jenkins-test-harness:2.8',
                "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}",
                'org.jenkins-ci.plugins.workflow:workflow-job:2.10@jar',
                'org.jenkins-ci.plugins.workflow:workflow-api:2.18@jar',
                'org.jenkins-ci.plugins.workflow:workflow-step-api:2.10@jar',
                'org.jenkins-ci.plugins:structs:1.7@jar',
                'org.jenkins-ci.plugins:scm-api:2.0.8@jar'

    testPlugins 'org.jenkins-ci.modules:pubsub-light:1.3',
                'org.jenkins-ci.plugins:sse-gateway:1.10',
                'org.jenkins-ci.ui:ace-editor:1.1',
                'org.jenkins-ci.ui:jquery-detached:1.2.1',
                'org.jenkins-ci.plugins:structs:1.7',
                'org.jenkins-ci.plugins.workflow:workflow-step-api:2.10',
                'org.jenkins-ci.plugins.workflow:workflow-scm-step:2.4',
                'org.jenkins-ci.plugins:scm-api:2.0.8',
                'org.jenkins-ci.plugins.workflow:workflow-api:2.18',
                'org.jenkins-ci.plugins.workflow:workflow-support:2.14',
                'org.jenkins-ci.plugins.workflow:workflow-job:2.10',
                'org.jenkins-ci.plugins.workflow:workflow-basic-steps:2.4',
                'org.jenkins-ci.plugins:pipeline-build-step:2.4',
                'org.jenkins-ci.plugins:pipeline-stage-step:2.2',
                'org.jenkins-ci.plugins.workflow:workflow-durable-task-step:2.12',
                'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5',
                'org.jenkins-ci.plugins.workflow:workflow-cps:2.36'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'

    doLast {
        def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.')-1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins

    exclude '**/XmlOutput.class'

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath

    testLogging {
        showStandardStreams = true
    }
}
